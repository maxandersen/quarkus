ifdef::context[:parent-context: {context}]
[id="important-gradle-configuration-points_{context}"]
= Important Gradle configuration points
:context: important-gradle-configuration-points

Similar to the Maven configuration, when using Gradle, the following modifications are required when Kotlin is selected:

* The `quarkus-kotlin` artifact is added to the dependencies. This artifact provides support for Kotlin in the live reload mode (more about this later on)
* The `kotlin-stdlib-jdk8` is also added as a dependency.
* The Kotlin plugin is activated, which implicitly adds `sourceDirectory` and `testSourceDirectory` build properties to point to Kotlin sources (`src/main/kotlin` and `src/test/kotlin` respectively)
* The all-open Kotlin plugin tells the compiler not to mark as final, those classes with the annotations highlighted (customize as required)
* When using native-image, the use of http (or https) protocol(s) must be declared
* An example configuration follows:

[source,groovy,subs="attributes+"]
----
plugins {
    id 'java'
    id 'io.quarkus' version '{quarkus-version}' // <1>

    id "org.jetbrains.kotlin.jvm" version "{kotlin-version}" // <2>
    id "org.jetbrains.kotlin.plugin.allopen" version "{kotlin-version}" // <2>
}

repositories {
    mavenCentral()
}

group = '...' // set your group
version = '1.0.0-SNAPSHOT'

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:{kotlin-version}'

    // <3>
    implementation enforcedPlatform('io.quarkus:quarkus-bom:{quarkus-version}')
    implementation 'io.quarkus:quarkus-resteasy'
    implementation 'io.quarkus:quarkus-resteasy-jsonb'
    implementation 'io.quarkus:quarkus-kotlin'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

test {
    useJUnitPlatform()
    exclude '**/Native*'
}

buildNative {
    enableHttpUrlHandler = true
}

allOpen { // <4>
    annotation("javax.ws.rs.Path")
    annotation("javax.enterprise.context.ApplicationScoped")
    annotation("io.quarkus.test.junit.QuarkusTest")
}
----

[arabic]
<1> The Quarkus plugin needs to be applied.
<2> The Kotlin plugin version needs to be specified.
<3> We include the Quarkus BOM using Gradle's link:https://docs.gradle.org/5.4.1/userguide/managing_transitive_dependencies.html#sec:bom_import[relevant syntax]
<4> The all-open configuration required, as per Maven guide above

or, if you use the Gradle Kotlin DSL:

[source,kotlin,subs="attributes+"]
----
import io.quarkus.gradle.tasks.QuarkusNative
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

group = "..."
version = "1.0.0-SNAPSHOT"

plugins {
    java
    id("io.quarkus") version "{quarkus-version}" // <1>

    kotlin("jvm") version "{kotlin-version}" // <2>
    id("org.jetbrains.kotlin.plugin.allopen") version "{kotlin-version}" // <2>
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(kotlin("stdlib-jdk8"))

    // <3>
    implementation(enforcedPlatform("io.quarkus:quarkus-bom:${quarkusVersion}"))
    implementation("io.quarkus:quarkus-kotlin")
    implementation("io.quarkus:quarkus-resteasy")
    implementation("io.quarkus:quarkus-resteasy-jsonb")

    testImplementation("io.quarkus:quarkus-junit5")
    testImplementation("io.rest-assured:rest-assured")
}

tasks {
    named<QuarkusNative>("buildNative") {
        setEnableHttpUrlHandler(true)
    }
}

allOpen { // <4>
    annotation("javax.ws.rs.Path")
    annotation("javax.enterprise.context.ApplicationScoped")
    annotation("io.quarkus.test.junit.QuarkusTest")
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

val compileKotlin: KotlinCompile by tasks
compileKotlin.kotlinOptions {
    jvmTarget = "1.8"
}

val compileTestKotlin: KotlinCompile by tasks
compileTestKotlin.kotlinOptions {
    jvmTarget = "1.8"
}
----

[arabic]
<1> The Quarkus plugin needs to be applied.
<2> The Kotlin plugin version needs to be specified.
<3> We include the Quarkus BOM using Gradle's link:https://docs.gradle.org/5.4.1/userguide/managing_transitive_dependencies.html#sec:bom_import[relevant syntax]
<4> The all-open configuration required, as per Maven guide above


ifdef::parent-context[:context: {parent-context}]
ifndef::parent-context[:!context:]